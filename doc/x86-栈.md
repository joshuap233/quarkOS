> > **堆栈**（英语：stack）又称为**栈**或**堆叠**，是[计算机科学](https://zh.wikipedia.org/wiki/計算機科學)中的一种[抽象资料类型](https://zh.wikipedia.org/wiki/抽象資料型別)，只允许在有序的线性资料集合的一端（称为堆栈顶端，英语：top）进行加入数据（英语：push）和移除数据（英语：pop）的运算。因而按照后进先出（LIFO, Last In First Out）的原理运作

栈可能是我们学习、使用的第一个数据结构，并且用途相当广泛，得益于出栈入栈 O(1) 的复杂度，笔者实现的 QuarkOS 曾经使用栈作为物理内存管理器，但这里要说的是 x86 的栈与 __cdecl 调用约定。

当 gcc 编译器将源码翻译成汇编时，函数调用者(caller)与被调用方(callee)之间需要传递参数，返回值，存储 callee 返回地址，那么存储在哪里合适呢？好的选择自然是寄存器，但由于函数需要传递的参数个数可能远超寄存器的数量，因此 __cdecl 规范规定 x86 使用内存（堆栈）传递参数，使用 eax 寄存器传递返回值。

我们写一小段 c 代码，使用 gcc 翻译成汇编，来了解调用规范：

```c
int test(int i){
  int j = 0;
  j++;
  i++;
  return i;
}

int main(){
	int i =0;
	i++;
	test(i);
	return 0;
}
```



输入 `i686-elf-gcc -S name.c ` 翻译成汇编，笔者使用 i686-elf-gcc 作为 gcc 编译器，读者可以自行选用合适的 c 编译器，但一定要翻译成 x86 汇编代码而非其他平台代码。



下面是翻译的结果：

```assembly
	.file	"test.c"
	.text
	.globl	test
	.type	test, @function
test:
	pushl	%ebp
	movl	%esp, %ebp
	subl	$16, %esp
	
	# 处理本地变量
	movl	$0, -4(%ebp)
	addl	$1, -4(%ebp)
	addl	$1, 8(%ebp) 
    
	movl	8(%ebp), %eax # 保存返回值到 eax
	leave   
	# leave 分为两步:
	# pop %ebp (进入函数时将 ebp 压栈)
    # mov %ebp,%esp  
	
	ret
	.size	test, .-test
	
	.globl	main
	.type	main, @function
main:
	# main 是 caller 也是 callee
	# ebp 压栈, esp 加载到 ebp
	pushl	%ebp
	movl	%esp, %ebp
	#设置 ebp esp 间可用空间为 16 字节
	subl	$16, %esp
	
	# 处理本地变量
	movl	$0, -4(%ebp)
	addl	$1, -4(%ebp)
	
	# 参数压栈
	pushl	-4(%ebp)
	call	test
	
	addl	$4, %esp 
	# test 函数 leave 指令将 ebp 加载到 esp, 
	# esp 加上 test 函数移动的 ebp 次数 * 4 即为 原来的 esp,
	# call 返回前 (ebp指向返回地址, ebp +4 指向参数)
	movl	$0, %eax # 返回值
	leave
	ret
	.size	main, .-main
	.ident	"GCC: (GNU) 10.2.0"
```

忽略掉  .file 、.text ，这些伪指令， 直接看到 

```assembly
	pushl	%ebp
	movl	%esp, %ebp
	subl	$16, %esp
```

  x86 架构使用 ss 作为栈段寄存器，使用 esp 作为栈指针寄存器，并且栈从高地址向低地址扩展，而 pushl 指令被 cpu 读入执行后，esp - 4 ，元素入栈，esp 被称为栈指针（x86 架构），而 ebp 称为栈帧（__cdecl 规范规定），ebp 与 esp 之间的固定区域叫 red zone。接下来使用 ebp 访问栈内数据，而非esp，可以看到函数调用时执行的第一条指令是 `pushl %ebp`，而 ebp 为 caller 的栈帧，因此可以追踪 ebp 来解析栈结构，这对于函数 DEBUG 有很大的帮助，后面会介绍具体实现。在某些平台上，gcc可能不会将 ebp 放到堆栈中，因此你可以添加 -fno-omit-frame-pointer 参数来防止优化，但  gcc 的优化参数 -O1 依然会去掉栈帧。

编译器将函数本地变量存入到栈中，且参数从右到左以此压栈，我们可以使用 `8(%ebp)` 访问第一个参数，`12(%ebp)` 访问第二个参数，以此类推（这里假设参数大小为 4 字节）。而 `4(%ebp)` 为 callee 的返回地址，`(%ebp)`为 caller 的 ebp 寄存器的值，我们可以构建函数调用栈：



<pre>
 .....   
参数3  （高地址）
-------
参数2
-------
参数1
-------
返回地址  (低地址)
-------
%ebp
</pre>





利用这个原理可以实现多线程的初始化，将线程的入口函数地址（直接对函数用取地址符即可）放到

栈顶，接下来时入口函数的参数1、2、2.....，调度器切换上下文时，将 esp 指针指向栈顶，当 switch 函数使用 ret 指令返回值，cpu 会将 esp 指向的地址弹出并赋给 eip指针，线程会从入口函数开始执行。

这是笔者曾经的 switch 函数实现：

[switch](https://github.com/joshuap233/quarkOS/blob/f5f4bdb9d995c4ab20620a40b0620327493b5544/src/sched/switch.s)

```assembly
# quarkOS/src/sched/switch.s
switch_to:
    # 存储上下文
    movl 4(%esp),%eax # 需要保存的上下文首地址
    movl 8(%esp),%ecx # 下一个线程上下文首地址
    movl %esp,%edx

    add  $24,%eax     # eax 为 context 尾地址
    movl %eax, %esp
    push %edi
    push %esi
    push %ebp
    push %ebx
    pushf
    push %edx      # 保存 esp

    # 任务切换
    movl %ecx, %esp

    pop  %edx   # 保存 esp
    popf
    pop  %ebx
    pop  %ebp
    pop  %esi
    pop  %edi

    mov  %edx, %esp

    ret # ret 指令会弹出栈顶元素，赋值给 eip 寄存器

```

前面我们说可以使用 ebp 解析栈来 DEBU，那么这是怎么实现的呢？

可以看到，栈顶为 caller 的 ebp，因此可以找到 caller 的函数栈栈顶，而 caller 的栈顶保存的是 caller 的 caller 的 ebp.... 一直往下往下遍历即可达到函数调用栈的栈底(需要将线程栈初始 ebp 置 0 以便于追踪底部)。而 ebp 之后为函数返回地址，也就是 caller 的函数地址（不是栈地址，而是 eip指向的指令地址），可以使用函数的地址到 elf header 的 symbol table 中找到函数名，然后打印。



下面是部分源码：

```c
// quarkOS/src/lib/qlib.c
void stack_trace() {
    // 要求栈底为 caller 的 ebp 与 eip
    struct stack_frame *stk;
    asm volatile("movl %%ebp,%0":"=r"(stk));
    while (stk->ebp) {
        char *name = cur_func_name((ptr_t) stk->eip);
        printfk("ip: [%x], func: [%s]\n", stk->eip, name == NULL ? "NULL" : name);
        stk = stk->ebp;
    }
}

//addr 为函数中指令的地址
char *cur_func_name(ptr_t addr) {
    for (uint32_t i = 0; i < (bInfo.symtab.size / bInfo.symtab.entry_size); ++i) {
        if (ELF32_ST_TYPE(bInfo.symtab.header[i].st_info) == STT_FUNC) {
            elf32_symbol_t entry = bInfo.symtab.header[i];
            if ((addr >= entry.st_value) && (addr <= entry.st_value + entry.st_size)) {
                return (void *) &bInfo.strtab.addr[entry.st_name];
            }
        }
    }
    return NULL;
}
```





最后介绍的是临时寄存器： `eax，ecx，edx`，这些寄存器用于存储函数调用的中间值，这些寄存器的值会因为函数调用而发生变化，以笔者个人的理解，通俗地说，两条 c 语句之间，这些寄存器的值都可能发生变化，因此如果非时钟中断函数调用 switch ，那么不需要保存临时寄存器，并且笔者实现的中断处理函数使用gcc 的特性 `__attribute__((interrupt))` 自动保存这些寄存器，因此笔者的 [struct context](https://github.com/joshuap233/quarkOS/blob/f5f4bdb9d995c4ab20620a40b0620327493b5544/src/include/sched/task.h) 结构并没有临时寄存器。



总结：

caller(调用者) 需要做的事情:

- 将 callee 参数从右到左压栈
- 执行 call 指令
- 将局部变量弹出,或者简单地移动 esp,忽略参数



callee (被调用的例程) 需要做的事情

- 将 caller 的 ebp 压栈(栈帧指针)
- 将当前 ebp 设置为 esp
- 将本地数据存储到栈上
- 弹出所有本地数据退出,或者将 ebp 加载到 esp 快速退出
- 弹出 ebp 并返回, 将返回值存储在 eax 中

gcc 编译器翻译时会自动做这些事情,但如果使用 c/c++ 调用汇编, 或者汇编调用 c/c++,不会有上面的部分,比如 c 调用汇编, 调用者不会有: `addl $4, %esp `, 而汇编代码保留原样 



cdecl 部分调用约定：

- 函数实参在线程栈上按照从右至左的顺序依次压栈。
- 函数返回值保存在寄存器 EAX/AX/AL 中
- 临时寄存器：eax、ecx、edx
- ret 指令弹出栈顶元素赋给 eip 指针，返回 caller



参考:

- [system_v abi](https://wiki.osdev.org/System_V_ABI)

- [stack](https://wiki.osdev.org/Stack)

- [x86 calling conventions](https://en.wikipedia.org/wiki/X86_calling_conventions)

  